import org.gradle.api.artifacts.maven.MavenDeployment

group 'org.caffetranslator'
version '0.9.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'

apply plugin: 'antlr'
apply plugin: 'application'

apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:$antlrVersion"
    compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
    compileOnly 'org.projectlombok:lombok:1.16.18'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

generateGrammarSource {
    arguments += ['-visitor']
}

jar {
    baseName = 'caffe-translator'
    appendix = 'slim'
    version = version
    manifest {
        attributes 'Main-Class': 'io.mxnet.caffetranslator.Launcher'
    }
}

shadowJar {
    baseName = 'caffe-translator'
    classifier = ''
    version = version
}

configurations {
    releaseJars
    ascSignatures
}

artifacts {
    releaseJars shadowJar
}

signing {
    sign configurations.releaseJars
}

uploadShadow {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment
                -> signing.signPom(deployment)
                configurations.releaseJars.artifacts.each { ra ->
                    def ascfile = file(ra.file.path + '.asc')
                    def ascArtifact = project.artifacts.add('ascSignatures', ascfile) {
                        classifier = ra.classifier
                        extension = ra.extension + '.asc'
                        type = ra.type + '.asc'
                    }
                    deployment.addArtifact(ascArtifact)
                }
            }

            repository(url: sonatypeRepositoryUrl) {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Caffe Translator'
                packaging 'jar'
                description 'Translate Caffe code to MXNet Python code'
                url 'http://caffetranslator.org'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'indhub'
                        name 'Indu Bharathi'
                    }
                }
            }
        }
    }
}

mainClassName = "io.mxnet.caffetranslator.Launcher"
